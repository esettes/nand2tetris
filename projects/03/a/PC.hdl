// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // increment register output
    Inc16(in=regout, out=incout);

    // register out = out + 1 if inc = 1
    Mux16(a=regout, b=incout, sel=inc, out=muxinc);

    // sets out to in if load = 1
    Mux16(a=muxinc, b=in, sel=load, out=outin);

    // sets out to 0 if reset = 1
    Mux16(a=outin, b=false, sel=reset, out=outzero);
    
    Register(in=outzero, load=true, out=out, out=regout);
}
